#!/bin/tcsh
#
# Usage:
#	deluser [-R] user ...
# -R : Remove directory and other files
#
set GROUPF = /etc/group
set PWORDF = /etc/passwd
set TMPF = /tmp/DU.$$.`whoami`
set MAIL = /usr/spool/mail
#
if (`whoami` != root) then
  echo 'DELUSER must only be run as root!'
  exit 1
endif
#
if ( "x$1" == x-R ) then
  set RM = yes
  shift
else
  set RM = no
endif
foreach USER ( $* )
  if ( x`pwget -n $USER -f '%n'` == x) then
    echo User $USER unknown
  else
#
# Removing Passwd entry
    pwget -x -n $USER >$TMPF
    set HDIR = `pwget -n $USER -f '%d'`
    cp $TMPF $PWORDF
#
# Updating groups
    sed s/,$USER\$// <$GROUPF | sed s/\\:$USER\$/\\:/ | sed s/,$USER,/,/ | \
    sed s/\\:$USER,/\\:/ > $TMPF
    cp $TMPF $GROUPF
    if ( $RM == yes) then  # Could back up before nuking, no?
      echo Nuking $USER -- $HDIR
      rm -rf $HDIR $MAIL/$USER
    endif
  endif
end
if ( -f  $TMPF) rm -f $TMPF
