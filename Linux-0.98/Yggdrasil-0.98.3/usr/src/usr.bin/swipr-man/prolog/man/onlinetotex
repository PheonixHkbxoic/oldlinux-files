#! /bin/sed -f
s/section\*/section/
s/^\.SS \(.*\)/\\subsection{\1}/
s/^\.S \(.*\)/\\section{\1}/
s/^\.BD$/\\begin{description}/
s/^\.ED$/\\end{description}\\pagebreak/
s/^\.BF$/\\begin{itemize}/
s/^\.EF$/\\end{itemize}\\pagebreak/
s/^\.IT \([^ ]*\) \(.*\) \([^ ]*\)/\\pagebreak\\item[{\\makebox[-6pt]{}}]{\\it \1 \\verb!\2! \3}\\hfil\\\\\\index{KEY \2\/2}%/
s/^\.PT \([^ ]*\) \(.*\)/\\pagebreak\\item[{\\makebox[-6pt]{}}]{\\it \\verb!\1! \2}\\hfil\\\\\\index{KEY \1\/1}%/
s/^\.D \([^ ]*\) \([1-9]*\) \(.*\)/\\predicate{:- \1}{\2}{\3}\\index{KEY \1\/\2}%/
s/^\.C \([^ ]*\) \([1-9]*\) \(.*\)/\\predicate{\1}{\2}{\3}\\index{KEY \1\/\2}%/
s/^\.C \([^ ]*\) 0/\\noargpredicate{\1}\\index{KEY \1\/0}%/
s/^\.P \([^ ]*\) \(.*\)/\\prefix{\1}{\2}\\index{KEY \1\/1}%/
s/^\.I \([^ ]*\) \(.*\) \([^ ]*\)/\\infix{\1}{\2}{\3}\\index{KEY \2\/2}%/
s/^\.F \(.*\) \([^ ]*\) \([1-9]*\) \(.*\)/\\function{\1}{\2}{\3}{\4}\\index{KEY \2()}%/
s/^\.F \(.*\) \([^ ]*\) 0/\\noargfunction{\1}{\2}\\index{KEY \2()}%/
s/^\.M \(.*\) \([^ ]*\) \([1-9]*\) \(.*\)/\\macro{\1}{\2}{\3}{\4}\\index{KEY \2()}%/
s/^\.M \(.*\) \([^ ]*\) 0/\\noargmacro{\1}{\2}\\index{KEY \2()}%/
t
s/\([a-z][a-zA-Z0-9_]*\/[0-9]\)/{\\tt \1}/g
s/\([a-z][a-zA-Z0-9_]*\/\[.*\]\)/{\\tt \1}/g
t

s/\\begin{code}/&/
t verb
b

:verb
n
s/\\version/Welcome to SWI-Prolog (version 1.5.0, August 1990)/
t verb
s/\\end{code}/&/
t
b verb
